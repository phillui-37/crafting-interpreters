expression = assignment ;
equality = comparison , { ( "!=" | "==" ) , comparison } ;
comparison = term , { ( ">" | ">=" | "<" | "<=" ) , term } ;
primary = "true" | "false" | "nil" | "this"
        | NUMBER | STRING
        | "(" , expression , ")"
        | IDENTIFIER
        | "super" , "." , IDENTIFIER ;
unary = ( "-" | "!" ) , unary  | call ;
call = primary , { "(" , [ arguments ] , ")" | "." , IDENTIFIER } ;
arguments = expression , { "," , expression } ;
factor = unary , { ( "/" | "*" ) , unary } ;
term = factor , { ( "-" | "+" ) , factor } ;

program = { declaration } , EOF ;
declaration = classDecl
            | funDecl
            | varDecl
            | statement ;
classDecl = "class" , IDENTIFIER , [ "<" , IDENTIFIER ] , "{" , [ function ] , "}" ;
funDecl = "fun" , function ;
function = IDENTIFIER , "(" , [ parameters ] , ")" , block ;
parameters = IDENTIFIER , { "," , IDENTIFIER } ;
varDecl = "var" , IDENTIFIER , [ "=" , expression ] , ";" ;
statement = exprStmt
          | forStmt
          | ifStmt
          | printStmt
          | returnStmt
          | whileStmt
          | block ;
returnStmt = "return" , [ expression ] , ";" ;
exprStmt = expression , ";" ;
printStmt = "print" , expression , ";" ;
assignment = [ call , "." ] , IDENTIFIER , "=" , assignment
           | logic_or ;
block = "{" , { declaration } , "}" ;
ifStmt = "if" , "(" , expression , ")" , statement , [ "else" , statement ] ;
logic_or = logic_and , { "or" , logic_and } ;
logic_and = equality , { "and" , equality } ;
whileStmt = "while" , "(" , expression , ")" , statement ;
forStmt = "for" , "(" , ( varDecl | exprStmt | ";" ) , [ expression ] , ";" , [ expression ] , ")" , statement ;